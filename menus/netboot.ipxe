#!ipxe

:start

set netboot_version 0.1.0
set ntp_server pool.ntp.org

:dhcp_retry
dhcp && goto dhcp_ok
echo "DHCP failed, retrying in 1 second"
sleep 1
goto dhcp_retry
:dhcp_ok

echo before NTP ${unixtime}
:ntp_retry
echo Setting clock with NTP ${ntp_server}
ntp ${ntp_server} && goto ntp_ok
echo "NTP failed, retrying in 1 second"
sleep 1
goto ntp_retry
:ntp_ok
echo after NTP ${unixtime}

set cls:hex 1b:5b:4a  # ANSI clear screen sequence - "^[[J"
set cls ${cls:string}

console --x 1024 --y 768
console --picture https://netboot.rstms.net/ipxe.png

:main_menu
clear main_menu_choice
#echo ${cls}
menu Reliance Systems Netboot v${netboot_version}
item --gap Default:
item mac_autoinstall MAC Address Autoinstall
item local Boot from local hdd
item --gap netboot.rstms.net
item rstms_openbsd75 OpenBSD 7.5
item rstms_debian12 Debian 12 bookworm
item --gap netboot.xyz
item netboot_xyz netboot.xyz menu
item --gap Tools:
item shell iPXE shell
choose --timeout 5000 --default mac_autoinstall main_menu_choice || goto local
#echo ${cls}
goto ${main_menu_choice}
goto main_menu


:error
echo Error occured, press any key to return to menu ...
prompt
goto main_menu

:mac_autoinstall
chain --replace https://netboot.rstms.net/ipxe/${net0/mac}.ipxe || error

:rstms_openbsd75
chain --replace https://netboot.rstms.net/ipxe/netboot-openbsd.ipxe || error
goto error

:rstms_debian12
chain --replace https://netboot.rstms.net/ipxe/netboot-debian.ipxe || error
goto error

:netboot_xyz
chain --replace https://boot.netboot.xyz
goto error

:local
echo Booting from local disks ...
exit 1

:shell
echo Type "exit" to return to menu.
shell
goto main_menu

:menu_exit
exit
